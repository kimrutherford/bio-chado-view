<%args>
$type
$rs
$list_title
@columns => ()
</%args>

<div class="rs_list">
% if ($list_title ne '') {
  <div>
    <span class="title"> <% $list_title %> </span>
  </div>
% }
% if (@field_labels == 0) {
  No results
% } else {
  <table class="list">
    <thead>
      <tr>
%   for my $head_col (@column_headings) {
        <td><span class="field_label"><% ucfirst $head_col %></span></td>
%   }
      <td>&nbsp;</td>
      </tr>
    </thead>
%   my $row_count = 0;
%   for my $object (@rows) {
    <tr class="<% $row_count % 2 == 0 ? 'even' : 'odd' %>">
%     for my $field_label (@field_labels) {
        <td><& field.mhtml, object => $object, field_label => $field_label &></td>
%     }
     <td><& field.mhtml, object => $object, field_label => "${type}_id" &></td>
    </tr>
%     $row_count++;
%   }
  </table>
% }
</div>

<%init>
my @field_labels = ();
my @column_headings = ();
my @rows = ();
my $first = 1;

my %field_infos = ();

if (defined $c->config()->{class_info}->{$type}->{field_infos}) {
  %field_infos = %{$c->config()->{class_info}->{$type}->{field_infos}};
}

while (my $obj = $rs->next) {
  if ($first) {
    if (!@columns) {
      @columns = $obj->columns();
    }

    for my $column_name (@columns) {
      next if $column_name eq 'created_stamp' or $column_name eq "${type}_id";
      push @field_labels, $column_name;
      my $column_heading = undef;
      if (defined $field_infos{$column_name}) {
        if (defined $field_infos{$column_name}->{display_title}) {
          $column_heading = $field_infos{$column_name}->{display_title};
        }
      }

      if (!defined $column_heading) {
        $column_heading = $column_name;
        $column_heading =~ s/_/ /g;
      }

      push @column_headings, $column_heading;
    }
    $first = 0;
  }
  push @rows, $obj;
}

</%init>
