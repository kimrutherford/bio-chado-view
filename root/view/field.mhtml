<%doc>
This component renders the value one field of an object.  If the field contains
the id of another object it will be rendered as a link to the
referenced object.  If the field is the primary id key of the current object,
it will be rendered as a 'details' link.
</%doc>

<%args>
$type
$object
$field_name
</%args>

<span class="field_value">
% if ($is_table_id_field) {
  <span class="this_id_field">
    <a href="<% $c->uri_for('/view/object', $type, $object->$table_id_field()) %>">[details&nbsp;...]</a>
  </span>
% } else {
%   if ($is_reference && $referenced_object) {
  <span class="ref_field">
  <a href="<% $c->uri_for('/view/object/', $referenced_object_id) %>">
    <& field.mhtml, type => $referenced_table, object => $referenced_object, field_name => $primary_key_name &>
  </a>
  </span>
%   } else {
%     if ($is_key_field) {
  <span class="attribute_field">
    <span class="display_key">
      <a href='<% $c->uri_for("/view/object", $type, $object->$table_id_field()) %>'>
        <% $field_value %>
      </a>
    </span>
%     } else {
    <% $field_value %>
%     }
  </span>
%   }
% }
</span>

<%init>
use SmallRNA::DB;

die "field_name undefined\n" unless defined $field_name;

my $parent_class_name = SmallRNA::DB::class_name_of_table($type);
my $table_id_field = "${type}_id";

my $field_value = $object->$field_name();

# set if this field is a reference:
my $primary_key_name = undef;
my $referenced_object = undef;
my $referenced_object_id = undef;
my $referenced_table = undef;

if (!defined $field_value) {
  $field_value = '';
}

my $is_table_id_field = 0;
my $is_reference = 0;
my $is_key_field = 0;

if ($field_name =~ /$table_id_field$/) {
  $is_table_id_field = 1;
} else {
  my $info_ref = $parent_class_name->relationship_info($field_name);
  if (defined $info_ref) {
    $is_reference = 1;
    my %info = %{$info_ref};
    my $referenced_class_name = $info{class};
    $referenced_table = SmallRNA::DB::table_name_of_class($referenced_class_name);

    $primary_key_name = $c->config()->{class_info}->{$referenced_table}->{display_field};

    if (!defined $primary_key_name) {
      die "no display_key_fields configuration for $referenced_table\n";
    }

    $referenced_object = $object->$field_name();

    my $primary_key = $referenced_table . '_id';
    if (defined $referenced_object) {
      $referenced_object_id = $referenced_object->$primary_key();
    }
  } else {
    my $display_key_field = $c->config()->{class_info}->{$type}->{display_field};

    if (defined $display_key_field && $field_name eq $display_key_field) {
      $is_key_field = 1;
    }
  }
}
</%init>
